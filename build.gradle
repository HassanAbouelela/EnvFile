plugins {
    id "java"
    id "idea"
    id "org.jetbrains.intellij" version "1.8.1"
    id "pl.allegro.tech.build.axion-release" version "1.14.0"
}

group "net.ashald"

scmVersion {
    tag {
        prefix.set("v")
        versionSeparator.set("")
    }
}

allprojects {
    repositories {
        mavenCentral()
    }
    project.version = scmVersion.version

    ext.jetbrains = [
            version : "2022.2",
            pycharm : "PythonCore:222.3345.40",
            rubymine: "org.jetbrains.plugins.ruby:222.3345.16",
            goland  : "org.jetbrains.plugins.go:222.3345.90",
            scala   : "org.intellij.scala:2022.2.3"
    ]
}

intellij {
    version.set("GO-2022.2")
    pluginName.set("EnvFile")
    sameSinceUntilBuild.set(false)
    updateSinceUntilBuild.set(false)
}

dependencies {
//    implementation project(":envfile-products-idea")
    implementation project(":envfile-products-pycharm")
//    implementation project(":envfile-products-rubymine")
//    implementation project(":envfile-products-goland")
}

wrapper {
    gradleVersion = "7.5.1"
}

def prj = ".idea/"

idea {
    module.iml {
      generateTo = file(prj)

      withXml {
          it.node.@type = "PLUGIN_MODULE"
          it.node.appendNode('component', [name: 'DevKit.ModuleBuildProperties', url: 'file://$MODULE_DIR$/src/main/resources/META-INF/plugin.xml'])
      }
    }
}

runIde {
    intellij {
        autoReloadPlugins = true
    }
}

task setup {
    dependsOn ideaModule, ideaProject
    // overriding idea.project.outputFile changes relative path calculation so it's easier to just move a file
    doLast {
       copy {
          from '.'
          into prj
          include "${project.name}.ipr"
          rename { "modules.xml" }
       }
       project.delete "${project.name}.ipr"
     }
}
